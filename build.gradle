apply plugin: 'application'
apply plugin: 'eclipse'

mainClassName = 'gl8080.byteman.puzzle.Main'

startScripts {
    applicationName = 'puzzle'
}

startScripts.with {
    doLast {
        def unixBytemanJar = '`dirname $0`/../lib/byteman-3.0.2.jar'
        unixScript.text = unixScript.text.replaceAll('DEFAULT_JVM_OPTS=""',
                /DEFAULT_JVM_OPTS="-javaagent:${unixBytemanJar}=listener:true,boot:${unixBytemanJar}"/
                + '\nPUZZLE_OPTS=-Dorg.jboss.byteman.transform.all')
        def winBytemanJar = /%~dp0..\\lib\\byteman-3.0.2.jar/;
        windowsScript.text = windowsScript.text.replaceAll('set DEFAULT_JVM_OPTS=',
                /set DEFAULT_JVM_OPTS="-javaagent:${winBytemanJar}=listener:true,boot:${winBytemanJar}"/
                + '\r\nset PUZZLE_OPTS=-Dorg.jboss.byteman.transform.all')
    }
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'
compileJava.options.encoding = 'UTF-8'

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.jboss.byteman:byteman:3.0.2'
    compile 'org.jboss.byteman:byteman-submit:3.0.2'
    testCompile 'junit:junit:4.12'
}

eclipse.project.name = 'byteman-puzzle'

task wrapper(type: Wrapper) {
    gradleVersion = '2.9'
}